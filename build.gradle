apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"

group = "com.glookast.settings"
version = "0.1.0-SNAPSHOT"

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

jar {
    manifest {
        attributes "Implementation-Version": version
    }
}

repositories {
    mavenCentral()
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.11.1'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.11.1'

    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.11.1'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.11.1'

    compile group: 'commons-validator', name: 'commons-validator', version: '1.6'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name "commons-settings"
                packaging "jar"
                // optionally artifactId can be defined here 
                description "Helper classes with Jackson annotations for easy interfacing with Glookast Products"
                url "http://github.com/glookast/commons-settings"

                scm {
                    connection "scm:git:git://github.com/glookast/commons-settings.git"
                    developerConnection "scm:git:ssh://github.com:glookast/commons-settings.git"
                    url "http://github.com/glookast/commons-settings/tree/master"
                }

                licenses {
                    license {
                        name "The Apache License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id "tiagoafpereira"
                        name "Tiago Pereira"
                        email "tiago.pereira@glookast.com"
                    }
                }
            }
        }
    }
}